apply plugin: 'java'
apply plugin: 'maven'

group = 'org.maydesk'
sourceCompatibility = JavaVersion.VERSION_1_4
def cloudbeesAccountName = 'maydesk'
def cloudbeesUsername = 'maydesk-deploy'
def cloudbeesPassword = 'secret111'

configurations {
	deployerJars
	testCompile
}

sourceSets {
	main {
		java {
			srcDir 'src/server-java/app'
			srcDir 'src/server-java/webcontainer'
			srcDir 'src/server-java/model'
			srcDir 'src/server-java/receiver'
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url "http://repository-maydesk.forge.cloudbees.com/snapshot"
	}
}

dependencies {
	compile 'org.maydesk:echo3-app:3.0-SNAPSHOT'
	compile 'org.maydesk:echo3-webcontainer:3.0-SNAPSHOT'
	compile fileTree(dir: 'lib')
	compile "javax.servlet:servlet-api:2.+"
	deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'
	deployerJars 'org.apache.maven:maven-ant-tasks:2.1.0'
}

ant.importBuild 'gradleWrapper.xml'

// Ant build file needs the varialbes to point to the correct location
// It's a bit of a hack. But works well for now. If a full migration is done this won't be needed
ant.properties['servlet.lib.jar'] = configurations.compile.asPath
ant.properties['echo3.app.lib.jar'] = configurations.compile.asPath
ant.properties['echo3.webcontainer.lib.jar'] = configurations.compile.asPath

defaultTasks 'uploadArchives'

clean.dependsOn 'ant.clean'
uploadArchives.dependsOn 'ant.dist'

def model = ant.properties['jarfile.filetransfer.model']
def modelFile = file(ant.properties['dir.dist.lib'] + '/' + model)

def receiver = ant.properties['jarfile.filetransfer.receiver']
def receiverFile = file(ant.properties['dir.dist.lib'] + '/' + receiver)

def fileTransferApp = ant.properties['jarfile.filetransfer.app']
def fileTransferAppFile = file(ant.properties['dir.dist.lib'] + '/' + fileTransferApp)

def webcontainer = ant.properties['jarfile.filetransfer.webcontainer']
def fileTransferWebContainerFile = file(ant.properties['dir.dist.lib'] + '/' + webcontainer)

// Removing .jar from the name
model = model.substring(0, model.length() - 4)
receiver = receiver.substring(0, receiver.length() - 4)
fileTransferApp = fileTransferApp.substring(0, fileTransferApp.length() - 4)
webcontainer = webcontainer.substring(0, webcontainer.length() - 4)

artifacts {
	archives file: modelFile, name: model, type: 'jar'
	archives file: receiverFile, name: receiver, type: 'jar'
	archives file: fileTransferAppFile, name: fileTransferApp, type: 'jar'
	archives file: fileTransferWebContainerFile, name: webcontainer, type: 'jar'
}

uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			snapshotRepository(url:"dav:https://repository-${cloudbeesAccountName}.forge.cloudbees.com/snapshot/") {
				authentication(userName: cloudbeesUsername, password: cloudbeesPassword)
			}
			repository(url: "dav:https://repository-${cloudbeesAccountName}.forge.cloudbees.com/release/") {
				authentication(userName: cloudbeesUsername, password: cloudbeesPassword)
			}
			addFilter(model) {artifact, file ->
				artifact.name == model
			}
			addFilter(receiver) {artifact, file ->
				artifact.name == receiver
			}
			addFilter(fileTransferApp) {artifact, file ->
				artifact.name == fileTransferApp
			}
			addFilter(webcontainer) {artifact, file ->
				artifact.name == webcontainer
			}
			
			pom(model).version = ant.properties['release.version']
			pom(receiver).version = ant.properties['release.version']
			pom(fileTransferApp).version = ant.properties['release.version']
			pom(webcontainer).version = ant.properties['release.version']
		}
	}
}